buildscript {
    ext {
        awsLambdaJavaCoreVersion = '1.2.1'
        awssdkBomVersion = '2.17.156'
        commonsLang3Version = '3.12.0'
        awsLambdaJavaLog4j2Version='1.5.0'
        jacksonDatabindVersion = '2.13.2'
        junitBomVersion = '5.8.2'
        mockitoInlineVersion = '3.11.2'
        log4jBomVersion = '2.17.2'
        slf4jNopVersion = '1.7.30'
        dependencyManagementPluginVersion = '1.0.11.RELEASE'
        modulePluginVersion = '1.8.10'
    }
}

plugins {
    id 'io.spring.dependency-management' version "${dependencyManagementPluginVersion}" apply false
    id 'org.javamodularity.moduleplugin' version "${modulePluginVersion}" apply false
}

allprojects {
    apply plugin: 'java'

    group = 'org.ideaslabut'
    version = '1.0.0-SNAPSHOT'
    sourceCompatibility = '11'
}

afterEvaluate {
    subprojects {
        apply plugin: 'org.javamodularity.moduleplugin'
        apply plugin: 'io.spring.dependency-management'

        repositories {
            mavenCentral()
        }

        dependencyManagement {
            dependencies {
                dependency "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"
                dependency "com.amazonaws:aws-lambda-java-core:${awsLambdaJavaCoreVersion}"
                dependency "com.amazonaws:aws-lambda-java-log4j2:${awsLambdaJavaLog4j2Version}"
                dependency "org.apache.commons:commons-lang3:${commonsLang3Version}"
                dependency "org.slf4j:slf4j-nop:${slf4jNopVersion}"
            }

            imports {
                mavenBom "software.amazon.awssdk:bom:${awssdkBomVersion}"
                mavenBom "org.junit:junit-bom:${junitBomVersion}"
                mavenBom "org.apache.logging.log4j:log4j-bom:${log4jBomVersion}"
            }
        }

        dependencies {
            testImplementation 'org.junit.jupiter:junit-jupiter'
            testImplementation "org.mockito:mockito-inline:${mockitoInlineVersion}"
        }

        test {
            moduleOptions {
                runOnClasspath = true
            }
            useJUnitPlatform()
        }
    }
}